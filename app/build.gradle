/*
 *     Copyright 2017 Alex Almeida Tavella
 *
 *     Licensed under the Apache License, Version 2.0 (the "License");
 *     you may not use this file except in compliance with the License.
 *     You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 *     Unless required by applicable law or agreed to in writing, software
 *     distributed under the License is distributed on an "AS IS" BASIS,
 *     WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *     See the License for the specific language governing permissions and
 *     limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt' // Use experimental kapt implementation
apply plugin: 'io.fabric'

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

def isCi = 'true' == System.getenv('CI')
def preDexEnabled = 'true' == System.getProperty('pre-dex', 'true')

android {
  compileSdkVersion 26
  buildToolsVersion "26.0.1"
  defaultConfig {
    applicationId "br.com.alex.moov"
    minSdkVersion 16
    targetSdkVersion 26
    versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    externalNativeBuild {
      cmake {
        cppFlags "-std=c++11"
      }
    }

    dexOptions {
      // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
      preDexLibraries = preDexEnabled && !isCi
    }
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
  externalNativeBuild {
    cmake {
      path "CMakeLists.txt"
    }
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
    androidTest.java.srcDirs += 'src/androidTest/kotlin'
  }

  dataBinding { enabled = true }

  lintOptions {
    lintConfig file('quality/lint.xml')
  }

  packagingOptions {
    exclude 'LICENSE.txt'
  }
}

dependencies {
  compile fileTree(include: ['*.jar'], dir: 'libs')

  // Android
  def support_version = '26.0.0-beta2'
  implementation "com.android.support:appcompat-v7:$support_version"
  implementation "com.android.support:design:$support_version"
  implementation 'com.android.support.constraint:constraint-layout:1.1.0-beta1'

  // Crashlytics
  implementation('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
    transitive = true
  }

  // Databinding
  kapt "com.android.databinding:compiler:$android_plugin_version"
  kaptTest "com.android.databinding:compiler:$android_plugin_version"
  kaptAndroidTest "com.android.databinding:compiler:$android_plugin_version"

  // Kotlin
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  kapt 'com.android.databinding:compiler:3.0.0-alpha7'
  kaptTest 'com.android.databinding:compiler:3.0.0-alpha7'
  kaptAndroidTest 'com.android.databinding:compiler:3.0.0-alpha7'

  // Dependency Injection
  implementation 'com.google.dagger:dagger:2.11'
  kapt 'com.google.dagger:dagger-compiler:2.11'
  kaptTest 'com.google.dagger:dagger-compiler:2.11'
  kaptAndroidTest 'com.google.dagger:dagger-compiler:2.11'

  // Retrofit
  implementation 'com.squareup.retrofit2:retrofit:2.2.0'
  implementation 'com.squareup.retrofit2:converter-gson:2.2.0'
  implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
  implementation 'com.squareup.okhttp3:logging-interceptor:3.7.0'

  // Rx
  implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

  // Because RxAndroid releases are few and far between, it is recommended you also

  // explicitly depend on RxJava's latest version for bug fixes and new features.
  implementation 'io.reactivex.rxjava2:rxjava:2.1.0'

  // Image
  implementation 'com.facebook.fresco:fresco:1.0.1'

  // DbFlow
  def dbflow_version = "4.0.0-beta5"
  kapt "com.github.Raizlabs.DBFlow:dbflow-processor:${dbflow_version}"
  kaptTest "com.github.Raizlabs.DBFlow:dbflow-processor:${dbflow_version}"
  kaptAndroidTest "com.github.Raizlabs.DBFlow:dbflow-processor:${dbflow_version}"
  implementation "com.github.Raizlabs.DBFlow:dbflow-core:${dbflow_version}"
  implementation "com.github.Raizlabs.DBFlow:dbflow:${dbflow_version}"
  implementation "com.github.Raizlabs.DBFlow:dbflow-kotlinextensions:${dbflow_version}"

  // Memory Leak detection
  debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5'
  releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
  testImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'

  // Logging
  implementation 'com.jakewharton.timber:timber:4.5.1'
  testImplementation 'junit:junit:4.12'
  testImplementation 'org.mockito:mockito-core:2.7.22'
  androidTestImplementation 'com.android.support.test.espresso:espresso-core:2.2.2'
  androidTestImplementation "com.android.support:support-annotations:$support_version"
  androidTestImplementation 'org.mockito:mockito-core:2.7.22'

  // Kotlin argument capture for non-null methods
  testImplementation 'com.nhaarman:mockito-kotlin:1.4.0'
  testImplementation 'org.jetbrains.kotlin:kotlin-stdlib:1.1.3-2'
}

crashlytics {
  enableNdk true
  androidNdkOut 'src/main/cpp'
  androidNdkLibsOut 'src/main/cpp'
}

gradle.projectsEvaluated {
  tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xmaxerrs" << "500"
  }
}
